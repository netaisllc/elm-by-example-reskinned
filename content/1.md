# 1: Hello, World
----------

The first example will display the “Hello World” message. The program
**HelloWorld1.elm** is presented below.

## HelloWorld1.elm:

```
import Graphics.Element
main = Graphics.Element.show "Hello World"
```

The first line imports the <kbd>Graphics.Element</kbd> module, allowing us to
reference its functions in our program. We need the import, since our
program uses the <kbd>show</kbd> function that is defined in that module. We
reference a function by prefixing the function name with the module
name and the dot.

The meaning of an Elm program is defined by the <kbd>main</kbd> function. Every
program needs to define one. To define a function, we provide its name
followed by the <kbd>=</kbd> character and the function body. Functions may
have parameters, which are declared between the function name and the
equals sign. The <kbd>main</kbd> function does not have parameters.

The body of our function contains a call to the
<kbd>Graphics.Element.show</kbd> function with one argument — the “Hello World”
string. The string is delimited with the quotation marks. The function
argument is separated from the function name by a single space (more
spaces would do as well). Elm does not require enclosing function
arguments in parenthesis.

Before the program can be run, it needs to be compiled. If you create
a directory with the **HelloWorld1.elm** program in it, you can use the
following command to compile it:

<pre>elm make HelloWorld1.elm --output HelloWorld1.html</pre>

When you run that command the first time in that directory, it needs
to perform certain setup steps: download, configure and compile
standard library files.  Before doing that, it asks for
permission. You should answer <kbd>y</kbd> to proceed. The command will create
a file called **elm-package.json** and a folder called **elm-stuff** in
the current directory.

<pre>$ <b>elm make HelloWorld1.elm --output HelloWorld1.html</b>
Some new packages are needed. Here is the upgrade plan.

  Install:
    elm-lang/core 2.1.0

Do you approve of this plan? (y/n) y
Downloading elm-lang/core
Packages configured successfully!
Success! Compiled 32 modules.
Successfully generated HelloWorld1.html
</pre>

When the program is compiled again in the same folder, the setup is
not performed again.

<pre>$ <b>elm make HelloWorld1.elm --output HelloWorld1.html</b>
Success! Compiled 1 modules.
Successfully generated HelloWorld1.html
</pre>

The compiler builds the **HelloWorld1.html** file. You can now open that
file using your favorite browser. Click on <a href='content/worked/HelloWorld1.html' target="_new">HelloWorld1.html</a> to see
the program output.


The **elm-package.json** file contains information related to the Elm
project. Its content may look like this:


<pre>{
    "version": "1.0.0",
    "summary": "helpful summary of your project, less than 80 characters",
    "repository": "https://github.com/USER/PROJECT.git",
    "license": "BSD3",
    "source-directories": [
        "."
    ],
    "exposed-modules": [],
    "dependencies": {
        "elm-lang/core": "2.1.0 &lt;= v &lt; 3.0.0"
    },
    "elm-version": "0.15.1 &lt;= v &lt; 0.16.0"
}
</pre>

You may change its content manually. It can also be modified by elm
tools: <kbd>elm-make</kbd>, that we have just met, and <kbd>elm-package</kbd>, that we
will meet soon.

Since we have used the <kbd>show</kbd> function, our program shows the “Hello
World” message using the default font and it encloses it in quotation
marks. What if we want to use a different font and get rid of the
quotations marks?  The **HelloWorld2.elm** program, presented below,
shows one way of styling the message in a custom way. You can see it
in action here: <a href='content/worked/HelloWorld2.html' target="_new">HelloWorld2.html</a>.


## HelloWorld2.elm:
```
module HelloWorld2 where


import Color exposing (blue)
import Graphics.Element exposing (..)
import Text


main : Element
main =
    Text.fromString "Hello World"
        |> Text.color blue
        |> Text.italic
        |> Text.bold
        |> Text.height 60
        |> leftAligned
```

The program begins with a module declaration. The declaration consists
of the <kbd>module</kbd> keyword followed by the module name and the <kbd>where</kbd>
keyword, which marks the beginning of the module body. If a program
does not start with the module declaration — like our
**HelloWorld1.elm** program — the module called <kbd>Main</kbd> is implicitly
assumed.

In order to add style to our message, we use functions from the <kbd>Text</kbd>
module from Elm’s standard library. To make that possible, we import
the <kbd>Text</kbd> module.

Instead of importing a module, we can alternatively import its
members, which introduces those members to the current namespace. We
do this by following the module name with the <kbd>exposing</kbd> keyword and a
list of imported values enclosed in parentheses. The first import
statement imports one member (<kbd>blue</kbd>) from the <kbd>Color</kbd>
module. Alternatively, we can replace the list with two dot
characters, thus imporing everything that the module exported. The
first import is using that syntax, importing everything that the
<kbd>Graphics.Element</kbd> exported, including the <kbd>Element</kbd> type.

The <kbd>main</kbd> function in this program has a type declaration (or in
other words, its signature). Signatures are optional and we did not
have it in our first program. In fact, the first versions of Elm did
not even provide a way of declaring function signatures. It is however
often a good idea to add them to our programs, as a way of documenting
its functions. The type declaration is given in the line preceding its
definition, and it consists of the function name followed by the colon
character and the function type. Our declaration states, that the
<kbd>main</kbd> function does not take any parameters and returns a value of
the <kbd>Element</kbd> type. Elements are “things” that Elm’s runtime know how
to display. The <kbd>Element</kbd> type name starts with a capital letter. In
fact, all type names in Elm must start with a capital letter.

The function definition must be consistent with its
signature. Otherwise Elm will complain by printing a compilation
error. For example, if we temporarily change the function type
declaration to <kbd>main : String</kbd> in our program (such a declaration
implies that the result of the <kbd>main</kbd> function is a string), the
compiler will complain:

<pre>$ <b>elm make HelloWorld2.elm --output HelloWorld2.html</b>
## ERRORS in HelloWorld2.elm ###################################################

-- TYPE MISMATCH ----------------------------------------------- HelloWorld2.elm

The type annotation for `main` does not match its definition.

9| main : String
          ^^^^^^
As I infer the type of values flowing through your program, I see a conflict
between these two types:

    String

    Element


Detected errors in 1 module.
</pre>

The <kbd>main</kbd> function body contains a set of expressions separated with
the <kbd>|></kbd> operators. What are they? They are in fact a way of function
application. If we have a function <kbd>f</kbd> taking one argument <kbd>a</kbd>, we can
call it using the <kbd>f a</kbd> syntax. But we can also swap the argument and
the function name and add the <kbd>|></kbd> operator, like so:

<pre>a |> f
</pre>

For greater readability, the program includes new line characters
before the <kbd>|></kbd> operators.

The body of our <kbd>main</kbd> function consists of several function
applications chained together. In fact, it could alternatively have
been written in the following way:

<pre>main = leftAligned (Text.height 60 (Text.bold (Text.italic (Text.color blue (Text.fromString "Hello World")))))
</pre>

Let’s now analyze what the function does. It starts with a call to the
<kbd>fromString</kbd> function, which transforms a string into a value of type
<kbd>Text</kbd>. It’s type looks like this:

<pre>fromString : String -> Text
</pre>

The <kbd>-></kbd> arrow separates the function argument type from the result
type. The <kbd>Text</kbd> value created by the <kbd>fromString</kbd> function has
certain default properties, like color or font size. The function
calls following the <kbd>fromString</kbd> call modify those properties,
creating new <kbd>Text</kbd> values, which differ in some respect from the old
value.

It is important to keep in mind, that Elm functions do not modify
their arguments in place. So, whenever I write about “modifying” the
function argument in some way, it is really a shortcut for saying that
the function returns a copy of the argument, which differs from the
original argument in some way.

The <kbd>color</kbd> function let us specify the color of the <kbd>Text</kbd> value. It
takes two arguments: the color and the text value and it returns a new
text value. Here is its type:

<pre>color : Color -> Text -> Text
</pre>

Notice how the <kbd>-></kbd> operator not only separates the result type of the
function from its arguments, but the same operator is also used for
separating one argument from the other.

The <kbd>italic</kbd> and <kbd>bold</kbd> functions, as their names suggest, modify the
text to be italic and bold. The call to the <kbd>height</kbd> function sets the
text height to 60 pixels. Like <kbd>color</kbd>, it is also a two-argument
function:

<pre>height : Float -> Text -> Text
</pre>

Finally, the <kbd>leftAligned</kbd> function turns a <kbd>Text</kbd> value into an
<kbd>Element</kbd> that can be displayed:

<pre>leftAligned : Text -> Element
</pre>

Let us get back to the signatures of the <kbd>color</kbd> and <kbd>height</kbd>
functions. Why is it, that the same operator — the <kbd>-></kbd> arrow — is
used for what might seem to be two different things: to separate one
function argument from the other and in the same time to separate the
arguments from the function result type? In fact, strictly speaking,
all Elm functions can only take at most one argument.

The <kbd>color</kbd>
function, which can in many circumstances be treated just as a
function taking two arguments, is more formally speaking a
one-argument function taking an argument of the <kbd>Color</kbd> type, that
returns another one-argument function, taking an argument of the
<kbd>Text</kbd> type, which in turn returns the result of type <kbd>Text</kbd>.

It might
seem to be a small technical distinction only and in many situations it
can be ignored. However, it also allows us to use a useful programming
technique of partially applying a function. Consider the
**HelloWorld3.elm** program, which displays the same text that
**HelloWorld2.elm** does (you can verify it here: <a href='HelloWorld3.html' target="_new">HelloWorld3.html</a>),
but is written in a slightly different way.


## HelloWorld3.elm

<pre>
module HelloWorld3 where


import Color exposing (blue)
import Graphics.Element exposing (..)
import Text as T


makeBlue : T.Text -> T.Text
makeBlue = T.color blue


main : Element
main =
    T.fromString "Hello World"
        |> makeBlue
        |> T.italic
        |> T.bold
        |> T.height 60
        |> leftAligned
</pre>

The first difference is the use of a <em>qualified</em> import. By suffixing
the import statement for the <kbd>Text</kbd> module with the <kbd>as T</kbd> clause, we
make the <kbd>Text</kbd> module available with the qualified name <kbd>T</kbd> instead
of the full name <kbd>Text</kbd>. The references to symbols defined in that
module must now be prefixed with <kbd>T.</kbd>.

The <kbd>main</kbd> function differs from its equivalent in the previous
program by using the auxiliary function <kbd>makeBlue</kbd> instead of directly
calling the <kbd>color</kbd> function with the <kbd>blue</kbd> argument.

Let us analyze the <kbd>makeBlue</kbd> function. Its body consists of applying
the <kbd>blue</kbd> value (of type <kbd>Color</kbd>) as the argument of the <kbd>color</kbd>
function. Let us recall the signature of <kbd>color</kbd>:

<pre>color : Color -> Text -> Text
</pre>

We have applied the first argument, but not the second. The result of
that application is a <kbd>Text -> Text</kbd> function. We have thus converted
a two-argument function <kbd>color</kbd> into a one-argument function
<kbd>makeBlue</kbd>, that transforms a <kbd>Text</kbd> value into another <kbd>Text</kbd> value,
which has its color set to blue.

The <kbd>Text</kbd> module is part of the Elm standard library. We have so far
only used a small subset of its functions, but it contains more of
them. If you want to verify what are the functions that a module
provide, try to find the module on the [Elm-lang](<http://package.elm-lang.org/)
web site.

The final example presented in this chapter — **HelloWorld4.elm** —
shows a web page, the content of which is specified using the
[Markdown](http://daringfireball.net/projects/markdown/) format.


## HelloWorld4.elm
<pre>
module HelloWorld4 where


import Markdown


main = Markdown.toElement """

# Hello World

This is the output of the *HelloWorld4.elm* program.

---
"""
</pre>

The body of the <kbd>main</kbd> function contains a call to the
<kbd>Markdown.toElement</kbd> function with one argument — a string containing
markdown syntax. The string is delimited with triple quotation mark
characters.


The line starting with a single hash <kbd>#</kbd> character is displayed as a
header (using the <kbd>h1</kbd> HTML tag) line.  The words enclosed in
asterisks are displayed in italic.  Finally, the three consecutive
dash characters are showed as a horizontal line. Our program only uses
a few selected Markdown features. You can read about others on the
[Markdown web site](http://daringfireball.net/projects/markdown/syntax).


The <kbd>Markdown</kbd> module does not belong to the Elm standard library. It
belongs to the **evancz/elm-markdown** package. We can install the
package with the following command:


<pre>$ <b>elm package install evancz/elm-markdown</b>
To install evancz/elm-markdown I would like to add the following
dependency to elm-package.json:

    "evancz/elm-markdown": "1.1.5 &lt;= v &lt; 2.0.0"

May I add that to elm-package.json for you? (y/n) y

Some new packages are needed. Here is the upgrade plan.

  Install:
    evancz/elm-html 4.0.1
    evancz/elm-markdown 1.1.5
    evancz/virtual-dom 2.0.0

Do you approve of this plan? (y/n) y
Downloading evancz/elm-html
Downloading evancz/elm-markdown
Downloading evancz/virtual-dom
Packages configured successfully!
</pre>

The command asked two questions and installed the package. It also
modified the **elm-package.json** file from the current folder:


<pre>{
    "version": "1.0.0",
    "summary": "helpful summary of your project, less than 80 characters",
    "repository": "https://github.com/USER/PROJECT.git",
    "license": "BSD3",
    "source-directories": [
        "."
    ],
    "exposed-modules": [],
    "dependencies": {
        "elm-lang/core": "2.1.0 &lt;= v &lt; 3.0.0",
        "evancz/elm-markdown": "1.1.5 &lt;= v &lt; 2.0.0"
    },
    "elm-version": "0.15.1 &lt;= v &lt; 0.16.0"
}
</pre>

Notice the additional line in the <kbd>dependencies</kbd> section. We can now
compile our program using <kbd>elm make</kbd>:


<pre>$ <b>elm make HelloWorld4.elm --output HelloWorld4.html</b></pre>

If at any time the example source code won't compile on your machine, try it in the online
[Elm dev environment](http://elm-lang.org/try) before submitting an issue.

### Next

The next chapter will show you how to make arithmetic calculations and
draw simple pictures. It will also present another tool provided by
Elm — the **Elm REPL**.


